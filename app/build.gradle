plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

version = '0.0.1-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.20.1'
    implementation 'com.salesforce.servicelibs:jprotoc:1.2.1'
    implementation 'org.projectlombok:lombok:1.18.24'
    implementation 'org.projectlombok:lombok:1.18.24'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'com.amazonaws:aws-java-sdk:1.12.220'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

application {
    mainClass = 'net.moznion.protoc.plugin.dynamodb.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
}

distZip {
    into("${archiveBaseName}-${archiveVersion}/bin") {
        fileMode = 0755
    }
}

distTar {
    compression = Compression.GZIP
    archiveExtension = 'tar.gz'
}

def devContainerName = 'protoc-gen-java-dynamodb:latest'

task buildDockerDevContainer(type: Exec) {
    workingDir '../'
    commandLine 'bash', '-c', "docker build -t ${devContainerName} -f devtools/Dockerfile ."
}

task gen(type: Exec) {
    workingDir '../'
    commandLine 'bash', '-c',
                "docker run -v \$(pwd):/protoc-gen-java-dynamodb -w /protoc-gen-java-dynamodb ${devContainerName} protoc --java_out=./app/src/main/java/ ./protos/options.proto"
}

task copyDepsToLib(type: Copy) {
    into "${buildDir}/lib"
    from configurations.runtimeClasspath
}

task copyBuiltJarToLib(type: Copy) {
    dependsOn jar
    into "${buildDir}/lib"
    from "${buildDir}/libs"
}

task buildForTest() {
    dependsOn compileJava, processResources, classes, jar, startScripts, distTar, distZip, assemble,
              copyBuiltJarToLib, copyDepsToLib
}

task cleanTestGeneratedFiles(type: Exec) {
    workingDir '../'
    commandLine 'bash', '-c',
                "rm -f ./app/src/test/java/net/moznion/protoc/plugin/dynamodb/*Proto.java"
}

task genTestProtos(type: Exec) {
    dependsOn buildForTest, cleanTestGeneratedFiles
    workingDir '../'
    commandLine 'bash', '-c',
                "docker run -v \$(pwd):/protoc-gen-java-dynamodb -w /protoc-gen-java-dynamodb ${devContainerName} protoc -I ./app/src/test/resources/protos/ --java_out=./app/src/test/java/ --plugin=protoc-gen-java-dynamodb=./app/build/scripts/app --java-dynamodb_out=./app/src/test/java ./app/src/test/resources/protos/basic_test_entity.proto"
}

task runDynamodbLocalContainer(type: Exec) {
    commandLine 'docker', 'run', '--rm', '-d', '-p', '38000:8000', 'amazon/dynamodb-local:latest'
}

tasks.test.dependsOn(genTestProtos)
